/*
 * Copyright (c) 2024 Snowflake Computing Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'com.diffplug.spotless:spotless-plugin-gradle:6.25.0'
    }
}

plugins {
    id 'idea'
    id 'eclipse'
}

allprojects {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }
}

subprojects {
    apply plugin: 'jacoco'
    apply plugin: 'java'
    apply plugin: 'com.diffplug.spotless'
    apply plugin: 'jacoco-report-aggregation'
    apply plugin: 'groovy'
    ext {
        jacksonVersion = '2.17.2'
        icebergVersion = '1.5.0'
        hadoopVersion = '3.3.6'
        dropwizardVersion = '4.0.7'
        assertJVersion = '3.25.3'
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs << '-Xlint:unchecked'
        options.compilerArgs << '-Xlint:deprecation'
    }

    project(':polaris-service') {
        apply plugin: 'application'
    }

    project(':polaris-core') {
        apply plugin: 'java-library'
    }

    dependencies {
        implementation "com.fasterxml.jackson.core:jackson-annotations:${jacksonVersion}"
        implementation 'com.google.guava:guava:33.0.0-jre'
        implementation "org.jetbrains:annotations:24.0.0"
        implementation 'org.slf4j:slf4j-api:2.0.12'
        compileOnly "com.github.spotbugs:spotbugs-annotations:4.8.5"

        testImplementation 'org.junit.jupiter:junit-jupiter:5.7.1'
        testImplementation 'org.assertj:assertj-core:3.25.3'
        testImplementation "org.mockito:mockito-core:5.11.0"

        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }

    task format {
        dependsOn 'spotlessApply'
    }

    test {
        useJUnitPlatform()
    }

    spotless {
        def disallowWildcardImports = {
            String text = it
            def regex = ~/import .*\.\*;/
            def m = regex.matcher(text)
            if (m.find()) {
                throw new AssertionError("Wildcard imports disallowed - ${m.findAll()}")
            }
        }
        java {
            target 'src/main/java/**/*.java', 'src/testFixtures/java/**/*.java', 'src/test/java/**/*.java'
            googleJavaFormat()
            indentWithSpaces(2)
            removeUnusedImports()
            endWithNewline()
            custom 'disallowWildcardImports', disallowWildcardImports
        }
    }
}

def projectName = rootProject.file("ide-name.txt").text.trim()
def ideName = "$projectName ${rootProject.version.toString().replace("^([0-9.]+).*", "\1")}"

if (System.getProperty("idea.sync.active").asBoolean()) {
    // There's no proper way to set the name of the IDEA project (when "just importing" or
    // syncing the Gradle project)
    def ideaDir = rootProject.layout.projectDirectory.dir(".idea")
    ideaDir.asFile.mkdirs()
    ideaDir.file(".name").asFile.text = ideName
}

eclipse { project { name = ideName } }
